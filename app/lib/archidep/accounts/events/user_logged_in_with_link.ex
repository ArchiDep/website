defmodule ArchiDep.Accounts.Events.UserLoggedInWithLink do
  @moduledoc """
  A user logged in with a login link. The user who logged in may be a student or
  a root user. Links are generated by root users in case a student cannot log in
  using their Switch edu-ID account.
  """

  use ArchiDep, :event

  alias ArchiDep.Accounts.Schemas.LoginLink
  alias ArchiDep.Accounts.Schemas.PreregisteredUser
  alias ArchiDep.Accounts.Schemas.UserAccount
  alias ArchiDep.Accounts.Schemas.UserSession
  alias ArchiDep.ClientMetadata
  alias Ecto.UUID

  @derive Jason.Encoder

  @enforce_keys [
    :login_link,
    :user_account,
    :session_id,
    :client_ip_address,
    :client_user_agent,
    :preregistered_user
  ]
  defstruct [
    :login_link,
    :user_account,
    :session_id,
    :client_ip_address,
    :client_user_agent,
    :preregistered_user
  ]

  @type t :: %__MODULE__{
          login_link: %{
            id: UUID.t()
          },
          user_account: %{
            id: UUID.t(),
            username: String.t() | nil,
            root: boolean()
          },
          session_id: UUID.t(),
          client_ip_address: String.t() | nil,
          client_user_agent: String.t() | nil,
          preregistered_user:
            %{
              id: UUID.t(),
              name: String.t(),
              email: String.t()
            }
            | nil
        }

  @spec new(LoginLink.t(), UserSession.t(), ClientMetadata.t()) :: t()
  def new(login_link, session, client_metadata) do
    %LoginLink{
      id: login_link_id,
      preregistered_user: preregistered_user
    } = login_link

    %UserSession{
      id: session_id,
      user_account: user_account
    } = session

    %UserAccount{
      id: user_account_id,
      username: username,
      root: root
    } = user_account

    client_ip_address =
      if client_metadata.ip_address,
        do: ClientMetadata.serialize_ip_address(client_metadata.ip_address),
        else: nil

    %__MODULE__{
      login_link: %{
        id: login_link_id
      },
      user_account: %{
        id: user_account_id,
        username: username,
        root: root
      },
      session_id: session_id,
      client_ip_address: client_ip_address,
      client_user_agent: client_metadata.user_agent,
      preregistered_user:
        case preregistered_user do
          %PreregisteredUser{
            id: preregistered_user_id,
            name: preregistered_user_name,
            email: preregistered_user_email
          } ->
            %{
              id: preregistered_user_id,
              name: preregistered_user_name,
              email: preregistered_user_email
            }

          nil ->
            nil
        end
    }
  end

  defimpl Event do
    alias ArchiDep.Accounts.Events.UserLoggedInWithLink

    @spec event_stream(UserLoggedInWithLink.t()) :: String.t()
    def event_stream(%UserLoggedInWithLink{user_account: %{id: user_account_id}}),
      do: "accounts:user-accounts:#{user_account_id}"

    @spec event_type(UserLoggedInWithLink.t()) :: atom()
    def event_type(_event), do: :"archidep/accounts/user-logged-in-with-link"
  end
end
