defmodule ArchiDep.Accounts.Events.UserRegisteredWithLink do
  @moduledoc """
  A new user account was registered using a login link generated by a root user.
  """

  use ArchiDep, :event

  alias ArchiDep.Accounts.Schemas.LoginLink
  alias ArchiDep.Accounts.Schemas.PreregisteredUser
  alias ArchiDep.Accounts.Schemas.UserAccount
  alias ArchiDep.Accounts.Schemas.UserSession
  alias Ecto.UUID

  @derive Jason.Encoder

  @enforce_keys [
    :login_link,
    :user_account,
    :session_id,
    :client_ip_address,
    :client_user_agent,
    :preregistered_user
  ]
  defstruct [
    :login_link,
    :user_account,
    :session_id,
    :client_ip_address,
    :client_user_agent,
    :preregistered_user
  ]

  @type t :: %__MODULE__{
          login_link: %{
            id: UUID.t()
          },
          user_account: %{
            id: UUID.t(),
            username: String.t() | nil,
            root: boolean()
          },
          session_id: UUID.t(),
          client_ip_address: String.t() | nil,
          client_user_agent: String.t() | nil,
          preregistered_user:
            %{
              id: UUID.t(),
              name: String.t(),
              email: String.t()
            }
            | nil
        }

  @spec new(LoginLink.t(), UserSession.t(), PreregisteredUser.t() | nil) :: t()
  def new(login_link, user_session, preregistered_user) do
    %LoginLink{
      id: login_link_id
    } = login_link

    %UserSession{
      id: session_id,
      client_ip_address: client_ip_address,
      client_user_agent: client_user_agent,
      user_account: user_account
    } = user_session

    %UserAccount{
      id: user_account_id,
      username: username,
      root: root
    } = user_account

    %__MODULE__{
      login_link: %{
        id: login_link_id
      },
      user_account: %{
        id: user_account_id,
        username: username,
        root: root
      },
      session_id: session_id,
      client_ip_address: client_ip_address,
      client_user_agent: client_user_agent,
      preregistered_user:
        case preregistered_user do
          %PreregisteredUser{
            id: preregistered_user_id,
            name: preregistered_user_name,
            email: preregistered_user_email
          } ->
            %{
              id: preregistered_user_id,
              name: preregistered_user_name,
              email: preregistered_user_email
            }

          nil ->
            nil
        end
    }
  end

  defimpl Event do
    alias ArchiDep.Accounts.Events.UserRegisteredWithLink

    @spec event_stream(UserRegisteredWithLink.t()) :: String.t()
    def event_stream(%UserRegisteredWithLink{user_account: %{id: user_account_id}}),
      do: "accounts:user-accounts:#{user_account_id}"

    @spec event_type(UserRegisteredWithLink.t()) :: atom()
    def event_type(_event), do: :"archidep/accounts/user-registered-with-link"
  end
end
